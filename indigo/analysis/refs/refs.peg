grammar Refs
  root        <- reference (and_or reference)* target? .* %root

                 # section 32
                 # section (a)
                 # section 32(a)(c)
                 # section 32.1a.2
  reference   <- unit WS+ main_ref WS* sub_refs? %reference

                 # section 32
                 # section 32A
                 # section 32.1a.2
  main_ref    <- digit alpha_num_dot* %main_ref

                 # (a)(ii) to (iv)
                 # (a), (b) and (f)(ii)
                 # (a)(ii), (b)(iii) and (v), and (d)
  sub_refs    <- sub_ref ((range / and_or) sub_ref)* %sub_refs

                 # (a)
                 # (a) (ii)
  sub_ref     <- num (WS* num)* %sub_ref

                 # (a)
                 # (a1)
                 # (a-bis)
                 # (iv)
  num         <- "(" alpha_num_dot+ ")" %num

                 # (a) to (b)
                 # (a), to (b)
  range       <- (WS* comma)? WS+ to WS+ %range

                 # (a) and (b)
                 # (a), or (b)
                 # (a), (b)
  and_or      <- ((WS* comma)? WS* _and WS+) %and_or
                 / ((WS* comma)? WS* _or WS+) %and_or
                 / (WS* comma WS*) %and_or

  target      <- of_this / of / thereof

                 # of the Act
  of_this     <- comma? WS* "of this" WS+ %of_this

                 # of the Act
  of          <- comma? WS* "of" WS+ %of

                 # thereof
  thereof     <- comma? WS* "thereof" %thereof

  # --------
  # terminals
  # --------

  _and        <- `and`
  _or         <- `or`
  comma       <- [,;]
  to          <- `to`
  # section, paragraph, etc.
  # TODO: include other languages
  unit        <- `section`

  digit       <- [0-9]
  alpha_num_dot <- [a-zA-Z0-9.-]
  WS          <- " " / "\t"
